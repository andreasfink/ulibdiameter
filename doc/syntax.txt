
the syntax for packet definitions in the Diameter documents is as following

<>  indicates the AVP is in a fixed position and mandatory
{}  indicates the AVP is mandatory but can be in any position
[]  indicates the AVP is optional. They can be in any position
*   indicates minimum/maximum values
    if an optional AVP requires these values
    and precedes it  1 * indicates
    the number of times the element is present in the message



command-def      = "<" command-name ">" "::=" diameter-message
command-name     = diameter-name
diameter-name    = ALPHA *(ALPHA / DIGIT / "-")
diameter-message = header   *fixed  *required *optional
header           = "<Diameter-Header:" command-id
[r-bit] [p-bit] [e-bit] [application-id]">"
application-id   = 1*DIGIT
command-id       = 1*DIGIT

; The Command Code assigned to the command.

r-bit            = ", REQ"
; If present, the 'R' bit in the Command
; Flags is set, indicating that the message
; is a request as opposed to an answer.

p-bit            = ", PXY"
; If present, the 'P' bit in the Command
; Flags is set, indicating that the message
; is proxiable.

e-bit            = ", ERR"
; If present, the 'E' bit in the Command
; Flags is set, indicating that the answer
; message contains a Result-Code AVP in
; the "protocol error" class.

fixed            = [qual] "<" avp-spec ">"
; Defines the fixed position of an AVP.

required         = [qual] "{" avp-spec "}"
; The AVP MUST be present and can appear
; anywhere in the message.






Fajardo, et al.              Standards Track                   [Page 38]

RFC 6733                 Diameter Base Protocol             October 2012


optional         = [qual] "[" avp-name "]"
; The avp-name in the 'optional' rule cannot
; evaluate to any AVP Name that is included
; in a fixed or required rule.  The AVP can
; appear anywhere in the message.
;
; NOTE:  "[" and "]" have a slightly different
; meaning than in ABNF.  These braces
; cannot be used to express optional fixed rules
; (such as an optional ICV at the end).  To do
; this, the convention is '0*1fixed'.

qual             = [min] "*" [max]
; See ABNF conventions, RFC 5234, Section 4.
; The absence of any qualifier depends on
; whether it precedes a fixed, required, or
; optional rule.  If a fixed or required rule has
; no qualifier, then exactly one such AVP MUST
; be present.  If an optional rule has no
; qualifier, then 0 or 1 such AVP may be
; present.  If an optional rule has a qualifier,
; then the value of min MUST be 0 if present.

min              = 1*DIGIT
; The minimum number of times the element may
; be present.  If absent, the default value is 0
; for fixed and optional rules and 1 for
; required rules.  The value MUST be at least 1
; for required rules.

max              = 1*DIGIT
; The maximum number of times the element may
; be present.  If absent, the default value is
; infinity.  A value of 0 implies the AVP MUST
; NOT be present.

avp-spec         = diameter-name
; The avp-spec has to be an AVP Name, defined
; in the base or extended Diameter
; specifications.

avp-name         = avp-spec / "AVP"
; The string "AVP" stands for *any* arbitrary AVP
; Name, not otherwise listed in that Command Code
; definition.  The inclusion of this string
; is recommended for all CCFs to allow for
; extensibility.

